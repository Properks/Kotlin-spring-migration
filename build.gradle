plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.25'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.25'
}


allprojects {

    group = 'org.jeongmo.migration'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'org.springframework.boot' // Spring Boot 플러그인 적용
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.plugin.jpa'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    kotlin {
        compilerOptions {
            freeCompilerArgs.addAll '-Xjsr305=strict'
        }
    }

    ext {
        set('springCloudVersion', "2025.0.0")
        set('logstashLogbackEncoder', "7.4")
        set('dockerUsername', 'properks')
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependency "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoder}"
        }
    }

    dependencies {
        implementation("org.namul:api-payload:0.7.4")
        implementation("org.jetbrains.kotlin:kotlin-reflect:1.9.25")
    }

    allOpen {
        annotation 'jakarta.persistence.Entity'
        annotation 'jakarta.persistence.MappedSuperclass'
        annotation 'jakarta.persistence.Embeddable'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

tasks.register('buildDomain') {
    group("Custom gradle")
    description("Cleans and builds bootJar for specified domain microservices.")
    def domainModules = project.subprojects.name
    domainModules.remove("common-config")

    domainModules.each { moduleName ->
        def project = findProject(":$moduleName")
        if (project != null) {
            def cleanTask = project.tasks.named("clean")
            def buildImageTask = project.tasks.named("bootBuildImage")
            if (cleanTask.isPresent() && buildImageTask.isPresent()) {
                dependsOn cleanTask
                dependsOn buildImageTask
                buildImageTask.configure {it.mustRunAfter cleanTask}
            }
            else if (buildImageTask.isPresent()) {
                dependsOn buildImageTask
            }
            else {
                logger.warn("Cannot find clean or bootJar task in $moduleName")
            }
        }
    }

    doLast {
        logger.info("Success clean and build")
    }
}